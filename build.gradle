plugins {
	id 'java'
	id 'groovy'
	id 'checkstyle'
	id 'jacoco'
	id 'idea'
	id 'net.linguica.maven-settings' version '0.5'
	id 'pl.allegro.tech.build.axion-release' version '1.7.1'
	id 'org.sonarqube' version '2.5'
	id 'com.bmuschko.nexus' version '2.3.1'
	id 'io.spring.dependency-management' version '1.0.3.RELEASE' apply false
	id 'org.springframework.boot' version '1.5.9.RELEASE' apply false
}

scmVersion {
	tag {
		prefix = rootProject.name
	}
	versionCreator 'versionWithBranch'
}

sonarqube {
	properties {
		property 'sonar.projectName', 'jenkins-demo'
		property 'sonar.exclusions', "**/generated-sources/**"
		property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
	}
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'groovy'
	apply plugin: 'checkstyle'
	apply plugin: 'net.linguica.maven-settings'
	apply plugin: 'com.bmuschko.nexus'

	group = 'com.github.tdakowicz'
	version = scmVersion.version

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	repositories {
		mavenLocal()
		mavenCentral()
		maven { name 'nexus-public'; url 'https://nexus.decerto.pl/content/groups/public' }
		maven { name 'nexus-decerto'; url 'https://nexus.decerto.pl/content/groups/decerto' }
	}

	test {
		testLogging {
			events "passed", "skipped", "failed"
			showStandardStreams = true
		}
	}

	checkstyle {
		configFile = rootProject.file('checkstyle.xml')
		toolVersion = '7.0'
	}
}

configurations {
	providedRuntime
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.0.1'
	distributionType = Wrapper.DistributionType.ALL
}